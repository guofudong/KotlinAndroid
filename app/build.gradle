apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.gfd.comswitch'
apply from: rootProject.file('and_res_guard.gradle')
apply from: rootProject.file('releaseinfo.gradle')
apply from: rootProject.file('libbase.gradle')

kapt {
    arguments {
        arg("moduleName", project.getName())
    }
}
android {

    signingConfigs {
        release {//release版的签名配置信息
            storeFile file(rootProject.ext.signConfigs.storeFile)
            storePassword rootProject.ext.signConfigs.storePassword
            keyAlias rootProject.ext.signConfigs.keyAlias
            keyPassword rootProject.ext.signConfigs.keyPassword
        }
    }

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
    }

    buildTypes {
        release {
            minifyEnabled true //混淆编译
            shrinkResources true //移除无用资源
            zipAlignEnabled true //zipalign优化
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField("boolean", "LOG_DEBUG", "false")//正式版不打印日志
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
            buildConfigField("boolean", "LOG_DEBUG", "true")
        }
    }

    //规定资源命名前缀为app_
    resourcePrefix "app_"

    //自定义输出apk名字
    applicationVariants.all {
        variant ->
            variant.outputs.all {
                if (variant.getBuildType().getName() == 'release') {
                    outputFileName = new File("app_v" + variant.versionName + "_" +
                            new Date().format("yyyy-MM-dd_HH", TimeZone.getTimeZone("UTC")) + "_" +
                            variant.getBuildType().getName() + ".apk")
                }
            }
    }

}

dependencies {
    api project(':Provider')
    kapt "com.alibaba:arouter-compiler:$arouter_compiler_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

//挂在到应用构建的过程中,执行module的 clean :解决Failed to delete \build\.......\classes.jar
this.project.afterEvaluate { project ->
    def buildTask1 = project.tasks.getByName('assembleDebug')
    def buildTask2 = project.tasks.getByName('assembleRelease')
    if (buildTask1 != null || buildTask2 != null) {
        clean.onlyIf {
            true
        }
    }
}